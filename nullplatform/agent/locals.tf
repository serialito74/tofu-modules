################################################################################
# Agent repository configuration
################################################################################

locals {

  # Parse and clean the primary scope repository
  nrn_without_namespace = join(":", slice(split(":", var.nrn), 0, 2))

  # Parse NRN parts into individual tags: "organization=123:account=456:namespace=789"
  nrn_parts = { for part in split(":", var.nrn) : split("=", part)[0] => split("=", part)[1] }
  nrn_tags = [
    for key in ["organization", "account", "namespace"] : {
      key   = key
      value = local.nrn_parts[key]
    } if contains(keys(local.nrn_parts), key)
  ]
  scope_list            = compact([trimspace(coalesce(var.agent_repos_scope, ""))])
  # Parse comma-separated extra repositories and clean whitespace
  repos_extra = compact([for s in var.agent_repos_extra : trimspace(s)])

  # Merge scope and extra repositories, removing duplicates
  final_repo_list = distinct(concat(local.scope_list, local.repos_extra))

  agent_repos = join(",", local.final_repo_list)
  tags        = join(",", [for k in sort(keys(var.tags_selectors)) : "${k}:${var.tags_selectors[k]}"])

  api_key = module.api_key.api_key

  default_args = [
    "--tags=$(TAGS)",
    "--apikey=$(NP_API_KEY)",
    "--runtime=host",
    "--command-executor-env=NP_API_KEY=$(NP_API_KEY)",
    "--command-executor-debug",
    "--webserver-enabled",
    "--command-executor-git-command-repos $(AGENT_REPOS)"
  ]

  cloud_args = {
    aws   = []
    gcp   = []
    azure = []
  }

  all_args = concat(local.default_args, lookup(local.cloud_args, var.cloud_provider, []))

  default_config = {
    NP_API_KEY              = local.api_key
    TAGS                    = local.tags
    AGENT_REPOS             = local.agent_repos
    CLUSTER_NAME            = var.cluster_name
    NAMESPACE               = var.namespace
    IMAGE_TAG               = var.image_tag
    DOMAIN                  = var.domain
    DNS_TYPE                = var.dns_type
    USE_ACCOUNT_SLUG        = var.use_account_slug
    IMAGE_PULL_SECRETS      = var.image_pull_secrets
    SERVICE_TEMPLATE        = var.service_template
    INITIAL_INGRESS_PATH    = var.initial_ingress_path
    BLUE_GREEN_INGRESS_PATH = var.blue_green_ingress_path
  }

  cloud_config = {
    aws = {
      AWS_IAM_ROLE_ARN = var.aws_iam_role_arn
    }

    gcp = {
      PRIVATE_GATEWAY_NAME = var.private_gateway_name
      PRIVATE_DOMAIN       = var.private_domain
    }

    azure = {
      PRIVATE_HOSTED_ZONE_RG = var.private_hosted_zone_rg
      PRIVATE_GATEWAY_NAME   = var.private_gateway_name
      PUBLIC_GATEWAY_NAME    = var.public_gateway_name
      RESOURCE_GROUP         = var.azure_resource_group
      AZURE_SUBSCRIPTION_ID  = var.azure_subscription_id
      AZURE_CLIENT_SECRET    = var.azure_client_secret
      AZURE_CLIENT_ID        = var.azure_client_id
      AZURE_TENANT_ID        = var.azure_tenant_id
    }
  }

  all_config = merge(
    local.default_config,
    lookup(local.cloud_config, var.cloud_provider, {}),
  )

  # Template único y simple
  nullplatform_agent_values = templatefile("${path.module}/templates/nullplatform_agent_values.tmpl.yaml", {
    args             = local.all_args
    config_values    = local.all_config
    image_tag        = var.image_tag
    aws_iam_role_arn = var.cloud_provider == "aws" ? var.aws_iam_role_arn : ""
    init_scripts     = var.init_scripts
  })
}